# A wedge, max thickness 10 cm thick cylinder in the PS

param -unset ZPOSITION=1965

param -unset THICKNESS=90

# INNER RADIUS 100 mm:
# 110 = 200 - 20*x
# 100 = 190 - 20*x
# 20*x = 200-110 = 90
# 20*x = 190-100 = 90
# x = 4.5

# INNER RADIUS 85 mm:
# 95 = 200 - 20*x
# 85 = 190 - 20*x
# 20*x = 200-95 = 105
# 20*x = 190-85 = 105
# x = 5.25

# param x=4.5
param x=5.25 # 85 mm

for i 0 1 2 3 4 5 6 7 8 9 10 11 12 13 14 15 16 17 18 19 20
    tubs Target outerRadius=200-$i*$x innerRadius=190-$i*$x length=$THICKNESS-$i*($THICKNESS-1)/20 \ 
	material=Be initialPhi=0. finalPhi=360. color=0,1,1 kill=0
	place Target x=0. z=$ZPOSITION rename=Target:$i 
endfor

# Place two virtualdetectors to catch everything entering the front and back faces of the target 

# This virtualdetector catches everything entering the target
# direction; same dimensions in x-y as the target; make it green
virtualdetector BeAbsorber_DetIn radius=200 length=1 color=0,1,0,0.3
# Place it flush with the front face of the target
place BeAbsorber_DetIn z=$ZPOSITION-$THICKNESS/2-1

# This virtualdetector catches everything exiting the target; make it red
virtualdetector BeAbsorber_DetOut radius=200 length=1 color=1,0,1,0.3
# Place it flush with the back face of the target
place BeAbsorber_DetOut z=$ZPOSITION+$THICKNESS/2+1